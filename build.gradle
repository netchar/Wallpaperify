apply plugin: 'com.github.ben-manes.versions'

buildscript {
    ext {
        appVersionName = '1.0.0'

        extractVersionCode = { versionName ->
            def (major, minor, patch) = versionName.toLowerCase().tokenize('.')
            return (major.toInteger() * 10000) + (minor.toInteger() * 100) + patch.toInteger()
        }
    }

    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.4.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.3.31"
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.21.0'
        classpath "android.arch.navigation:navigation-safe-args-gradle-plugin:1.0.0"
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }

    apply from: "$rootDir/ktlint.gradle"
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

dependencyUpdates.resolutionStrategy {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'preview', 'b', 'ea'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-+]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}
