apply plugin: 'com.github.ben-manes.versions'

ext {
    versions = [:]
    versions.compileSdk = 28
    versions.targetSdk = 28
    versions.minSdk = 21
    versions.buildTools = "28.0.3"
    versions.kotlin = "1.3.21"
    versions.coroutines = "1.1.1"
    versions.appcompat = "1.0.2"
    versions.constraintlayout = "1.1.3"
    versions.material = "1.0.0"
    versions.legacy = "1.0.0"
    versions.lifecycle = "2.0.0"
    versions.timber = "4.7.1"
    versions.dagger = "2.21"
    versions.retrofit = "2.5.0"
    versions.kotlinCoroutinesAdapter = "0.9.2"
    versions.converterMoshi = "2.5.0"
    versions.okhttp = "3.14.0"
    versions.threetenabp = "1.2.0"
    versions.moshi = "1.8.0"
    versions.kotshi = "1.0.6"
    versions.glide = "4.9.0"

    // test
    versions.runner = "1.1.1"
    versions.espresso = "3.1.1"
    versions.junit = "5.4.1"
    versions.mockk = "1.9.3"
}

buildscript {
    ext {
        extractVersionCode = { versionName ->
            def (major, minor, patch) = versionName.toLowerCase().tokenize('.')
            return (major.toInteger() * 10000) + (minor.toInteger() * 100) + patch.toInteger()
        }
    }

    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:3.3.2"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.3.21"
        classpath "de.mannodermaus.gradle.plugins:android-junit5:1.4.0.0"
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.21.0'
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

dependencyUpdates.resolutionStrategy {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'preview', 'b', 'ea'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-+]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}
